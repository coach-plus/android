apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    lintOptions {
        abortOnError false
    }
    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile rootProject.file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        vectorDrawables.useSupportLibrary = true
        applicationId "com.mathandoro.coachplus"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode 9
        versionName "1.1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [appName: "Coach+"]
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.config
        }
    }
    flavorDimensions 'tier'
    productFlavors {
        local {
            dimension 'tier'
            manifestPlaceholders = [appName: "Coach+ (Local)", appIcon: "ic_launcher_dev"]
            applicationId "com.mathandoro.coachplus.local"
            buildConfigField "String", "BASE_URL", '"http://192.168.0.115:4000"'
        }
        dev {
            dimension 'tier'
            manifestPlaceholders = [appName: "Coach+ (Dev)", appIcon: "ic_launcher_dev"]
            applicationId "com.mathandoro.coachplus.dev"
            buildConfigField "String", "BASE_URL", '"https://dev.coach.plus"'
        }
        beta {
            dimension 'tier'
            manifestPlaceholders = [appName: "Coach+ (Beta)", appIcon: "ic_launcher_beta"]
            applicationId "com.mathandoro.coachplus.beta"
            buildConfigField "String", "BASE_URL", '"https://beta.coach.plus"'
        }
        prod {
            dimension 'tier'
            manifestPlaceholders = [appName: "Coach+", appIcon: "ic_launcher_prod"]
            applicationId "com.mathandoro.coachplus"
            buildConfigField "String", "BASE_URL", '"https://coach.plus"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    androidTestImplementation('androidx.test.espresso:espresso-core:3.1.0-alpha4', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.transition:transition:1.1.0'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.google.firebase:firebase-core:17.0.1'
    implementation 'com.google.firebase:firebase-messaging:19.0.1'

    implementation 'com.getbase:floatingactionbutton:1.10.1'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.squareup.retrofit2:retrofit:2.2.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.2.0'
    implementation 'com.squareup.picasso:picasso:2.5.2'
    implementation 'com.google.code.gson:gson:2.8.5'

    api 'com.theartofdev.edmodo:android-image-cropper:2.8.0'


    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    // Because RxAndroid releases are few and far between, it is recommended you also
    // explicitly depend on RxJava's latest version for bug fixes and new features.
    // (see https://github.com/ReactiveX/RxJava/releases for latest 2.x.x version)
    implementation 'io.reactivex.rxjava2:rxjava:2.x.x'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:1.10.19'

}

apply plugin: 'com.google.gms.google-services'
